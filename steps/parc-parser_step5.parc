

parser {
	> ruleset equals ruleset
}

ruleset {
	rule+
}

rule {
	name > ruleprops group
}

ruleprops {
	> star? rulearrow?
}

rulearrow {
	arrow > name?
}

group {
	"{" > groupcontent "}"
	"(" > groupcontent ")"
}

groupcontent => {
	uncondbranch > "lb"?
	branch > groupcontenttail?
}

groupcontenttail => {
	"/" > groupcontent
	"lb" > groupcontent?
}

branch {
	offsets > expression? cond? brancharrow?
	expression > cond? brancharrow?
}

offsets {
	offset+
}

uncondbranch => branch {
	cond > brancharrow?
	brancharrow
}

cond {
	">" > expression*
}

brancharrow {
	arrow > name?
}

expression {
	offset
	unmoddedexpr > exprmod?
}

offset {
	number > offsettype literal
	offsettype > literal
}

unmoddedexpr => {
	colon > nongroupbase
	baseexpr
}

baseexpr => {
	nongroupbase / group
}

nongroupbase => {
	literal / name
}

literal => {
	dot / string / chars
}

exprmod => {
	star / plus / question
}



=====================



tokens* {
	> [ \t\r\n]* (grouportoken > [ \t\r\n]*)*
}

grouportoken* {
	grouptokens / token
}

grouptokens* {
	openbracket > [ \t\r\n]* groupcontent closebracket
}

groupcontent* {
	> (grouportoken > groupws?)*
}

groupws* {
	[ \t\r]+ > (lb > [ \t\r\n]*)?
	lb > [ \t\r\n]*
}

lb {
	"\n"
}

openbracket => {
	[{(]
}

closebracket => {
	[})]
}

token => {
	specialsyntax / string / chars / number / name
}

specialsyntax => {
	arrow / equals / star / plus / question / dot / offsettype / colon / [/>]
}

offsettype {
	[&!]
}

colon {
	[:;]
}

arrow {
	"=>"
}

equals {
	"="+
}

star {
	"*"
}

plus {
	"+"
}

question {
	"?"
}

dot {
	"."
}

name {
	[a-zA-Z_] > nametail?
}

nametail {
	[a-zA-Z0-9_]+
}

number {
	"-" > [0-9]+
	[0-9]+
}

string {
	"\"" > stritem* "\""
}

stritem {
	"\\" > . / [^"]
}

chars {
	"[" > charsitem* "]"
}

charsitem {
	"\\" > . / [^\]]
}

